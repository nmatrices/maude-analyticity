--- KB
load ./base .
load ./analiticity .

--- Sets for KB and dependencies 
fmod KB-SPEC is
    pr STATE .

    --- ---------------
    var V  : BValue .
    --- ---------------

    --- Sets 
    ops ALL D ND N I P PN : -> SetBValue .
    eq D = T, t, t1, t2 .
    eq ND = F, f, f1, f2 .
    eq N = T, t1, f2 ,f1 .
    eq I = F, f1, t2, t1 .
    eq P = T, t, f, f2 .
    eq PN = F, f, t, t2 .
    eq ALL = T, t, F, f, t2, f2, t1, f1 .

    --- Dependencies 
    op arrow : Value   -> SetBValue .
    eq arrow(T) = T, t .
    eq arrow(t) = T, t .
    eq arrow(t2) = f, f2 .
    eq arrow(f2) = t, t2 .
    eq arrow(f) = t, t2 .
    eq arrow(F) = F, f .

    op arrow2 : Value   -> SetBValue .
    op arrow2 : Value   -> SetBValue .
    eq arrow2(t) = f, f2 .
    eq arrow2(f) = F, f .
    eq arrow2(V) = empty [owise] .

    --- Support (values in the relCondition)
    op support : Value -> SetBValue .
    eq support(T) = T, t .
    eq support(t) = T, t, f, f2 .
    eq support(t1) = empty .
    eq support(t2) = f, f2 .
    eq support(f2) = t, t2 .
    eq support(f1) = empty .
    eq support(f) = F, f, t, t2 .
    eq support(F) = F, f .
endfm


view KB from TH-LOGIC to KB-SPEC is endv

--- Procedures for checking whether the matrix is OK
mod KB-CHECK is
    pr CHECK-REL{ KB } .

    --- --------------------
    vars V VNEW VNEW'  : BValue .
    var N         : Nat .
    var SV        : SetBValue . 
    vars ROWS ROWS' : Matrix .
    vars ASSG ASSG' : SetAssignment .
    vars ID ID' : Id .
    --- --------------------
    --- Checking if the matrix satisfies the conditions 
    op check : Matrix -> Bool .
    op checkRel : Row Matrix -> Bool .
    op checkArrow : Matrix Matrix -> Bool .

    eq check ( ([ ASSG ] : 1 , ROWS)) = checkRel( [ ASSG ] : 1, ROWS) and-then 
                                     checkArrow(( [ ASSG ] : 1      ), ([ ASSG ] : 1, ROWS)) .

    --- No more rows to be checked 
    eq checkRel ( [ ASSG ] : 1, empty) = true .
    --- A row generated from 1 must be checked 
    eq checkRel (  [ NEW : VNEW , ASSG  ] : 1,
                  ([ NEW : VNEW', ASSG' ] : N <- 1, ROWS)) = check-rel(VNEW, VNEW') and-then
       checkRel( [ NEW : VNEW, ASSG ] : 1, ROWS) .
endm

mod KB-CASE-BOX is
    pr KB-CHECK .

    --- --------------------
    vars V V' V'' : BValue .
    vars VA VC VNEW : BValue . 
    var ROWS  : Matrix .
    var ROW   : Row .
    vars ASSG ASSG' ASSG'' : SetAssignment .
    vars ID ID' ID'' : Id .
    --- --------------------

    --- Extending box 
    op box : BValue        -> BValue .
    op box1 : BValue        -> BValue .
    op box2 : BValue        -> BValue .
    op box3 : BValue        -> BValue .
    op box' : BValue        -> SetBValue .

    --- Extending the matrix
    --- box if not defined here since it is not used by rule value (overwritten here as value')

    eq box1(T) = t .
    eq box1(t) = f .
    eq box1(t2) = f .
    eq box1(f2) = t2 .
    eq box1(f) = F .
    eq box1(F) = F .
    eq box1(t1) = t1 .
    eq box1(f1) = t1 .


    eq box2(T) = T .
    eq box2(t) = f2 .
    eq box2(t2) = f2 .
    eq box2(f2) = t2 .
    eq box2(f) = F .
    eq box2(F) = F .
    eq box2(t1) = t1 .
    eq box2(f1) = t1 .

    eq box3(T) = t2 .
    eq box3(t) = F .
    eq box3(t2) = F .
    eq box3(f2) = t2 .
    eq box3(f) = F .
    eq box3(F) = F .
    eq box3(t1) = t1 .
    eq box3(f1) = t1 .


    op valid-case? : Matrix -> Bool .
    eq valid-case?( ROWS) = true .


    --- Checking arrow for t/f (Two arrows)
    ceq checkArrow([ NEW : V,  ASSG  ] : 1, 
                   (ROWS, 
                    [ NEW : V' ,  ASSG'   ] : ID', 
                    [ NEW : V'',  ASSG''  ] : ID'')) = true
        if V in (t,f) and-then V' in arrow(V) and-then  V'' in arrow2(V) .

    --- The other cases that need only one arrow 
    ceq checkArrow([ NEW : V,  ASSG  ] : 1, 
                   (ROWS, 
                    [ NEW : V' ,  ASSG'   ] : ID')) = true
        if not(V in (t,f)) and-then V' in arrow(V) .

    eq checkArrow(ROW, ROWS) = false [owise] .


    op two-values : Matrix -> Bool .
    ceq two-values(  ([ A : V,  ASSG  ] : ID, 
                      [ A : V', ASSG' ] : ID', ROWS)) = true 
         if V in (T, f2) and-then V' in (F, f, t, t2) .
    eq two-values(ROWS) = false [owise] .

    op all-t-f2 : Matrix -> Bool .
    ceq all-t-f2(  ([ A : V,  ASSG  ] : ID, ROWS)) = false
            if not ( V in (T, f2)) .
    eq all-t-f2(ROWS) = true [owise] .


    --- Customized rules for assigning values in all the cases

    --- The decision of the value taken in the other lines depend
    --- on the value in the first line. This is due to symmetry:
    --- if the value in the 1st is in the set {T, f2}, we are
    --- necessarily in the case box1 or box2. Otherwise, 
    --- we are necessarily in the case box1 or box3. 
    rl [value'] :  { ROWS, [ ASSG,  A : VA, NEW : ?f ] : 1 }  => 
        if two-values(ROWS) then
            { to-box(VA, ROWS), [ ASSG,  A : VA, NEW : box1(VA)  ] : 1 }
        else (if all-t-f2(ROWS) then 
                { to-box(VA, ROWS), [ ASSG,  A : VA, NEW : box2(VA)  ] : 1 }
             else
                { to-box(VA, ROWS), [ ASSG,  A : VA, NEW : box3(VA)  ] : 1 }
             fi)
       fi .

    --- rl [value'] :  A : VA, NEW : ?f'  => A : VA , NEW : ?[ box'(VA) ] .

    op to-box : BValue Matrix -> Matrix .
    eq to-box(V, empty) = empty .
    eq to-box(V, (  [ASSG, A : VA, NEW : ?f'  ] : ID , ROWS)) = 
                 (  [ASSG, A : VA, NEW : ?[ if (V in (T, f2)) then box2(VA) else box3(VA) fi,  box1(VA)] ] : ID), 
                  to-box(V, ROWS) .

endm 

view KB-Case-Box from TH-BOX to KB-CASE-BOX is endv

mod CHECK-ANALITICITY-BOX is
    pr ANALITICITY-BOX{KB, KB-Case-Box} .
endm

eof .

dsrew  in CHECK-ANALITICITY-BOX  : initBox using guess ! ; depC ; depA ; guess ! ; value' ! ; guess ! ; one(simplify) ! ; check .
dsrew  in CHECK-ANALITICITY-BOX  : initBox using guess ! ; depC ; depA ; guess ! ; value' ! ; guess ! ; one(simplify) ! ; check ; match S:FState .
