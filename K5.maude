--- Logic K5
load ./base .
load ./analiticity .

--- Sets for K5 and dependencies 
fmod K5-SPEC is
    pr STATE .

    --- ---------------
    var V  : BValue .
    --- ---------------

    --- Sets 
    ops ALL D ND N I P PN : -> SetBValue .
    eq D = T, t, t2, t1 .
    eq ND = F, f, f2, f1 .
    eq N = T, t1, f2, f1 .
    eq I = F, f1, t2, t1 .
    eq P = T, t, f, f2 .
    eq PN = F, f, t, t2 .
    eq ALL = T, t, F, f, t2, f2, t1, f1 .

    --- Dependencies 
    op arrow : Value   -> SetBValue .
    eq arrow(T) = T, t .
    eq arrow(t) = f .
    eq arrow(t2) = F, f .
    eq arrow(f2) = T, t .
    eq arrow(f) = t .
    eq arrow(F) = F, f .
    eq arrow(V) = empty [owise] .


    op arrow2 : Value   -> SetBValue .
    op arrow2 : Value   -> SetBValue .
    eq arrow2(t) = t .
    eq arrow2(f) = f .
    eq arrow2(V) = empty [owise] .

    --- Support (values in the relCondition)
    op support : Value -> SetBValue .
    eq support(T) = T, t .
    eq support(t) = t, f .
    eq support(t2) = F, f .
    eq support(f2) = T, t .
    eq support(f) = t, f .
    eq support(F) = F, f .
    eq support(t1) = empty .
    eq support(f1) = empty .


endfm

view K5 from TH-LOGIC to K5-SPEC is endv

--- Procedures for checking whether the matrix is OK
mod K5-CHECK is
    pr CHECK-REL{ K5 } .

    --- --------------------
    vars V VNEW VNEW'  : BValue .
    var N         : Nat .
    var SV        : SetBValue . 
    vars ROWS ROWS' : Matrix .
    vars ASSG ASSG' : SetAssignment .
    vars ID ID' : Id .
    --- --------------------
    --- Checking if the matrix satisfies the conditions 
    op check : Matrix -> Bool .
    op checkRel : Row Matrix -> Bool .
    op checkArrow : Matrix Matrix -> Bool .

    eq check ( ([ ASSG ] : 1 , ROWS)) = checkRel( [ ASSG ] : 1, ROWS) and-then 
                                     checkArrow(( [ ASSG ] : 1      ), ([ ASSG ] : 1, ROWS)) .

    --- No more rows to be checked 
    eq checkRel ( [ ASSG ] : 1, empty) = true .
    --- A row generated from 1 must be checked 
    eq checkRel (  [ NEW : VNEW , ASSG  ] : 1,
                  ([ NEW : VNEW', ASSG' ] : N <- 1, ROWS)) = check-rel(VNEW, VNEW') and-then
       checkRel( [ NEW : VNEW, ASSG ] : 1, ROWS) .
endm

mod K5-CASE-BOX is
    pr K5-CHECK .

    --- --------------------
    vars V V' V'' : BValue .
    vars VA VC VNEW : BValue . 
    var ROWS  : Matrix .
    var ROW   : Row .
    vars ASSG ASSG' ASSG'' : SetAssignment .
    vars ID ID' ID'' : Id .
    --- --------------------

    --- Extending box 
    op box : BValue        -> BValue .
    op box1 : BValue        -> BValue .
    op box2 : BValue        -> BValue .
    op box' : BValue        -> SetBValue .

    --- Extending the matrix
    --- box if not defined here since it is not used by rulu value (overwritten here as value')

    --- Case (3) in the PDF
    eq box1(T) = T .
    eq box1(t) = F .
    eq box1(t2) = F .
    eq box1(f2) = T .
    eq box1(f) = F .
    eq box1(F) = F .
    eq box1(t1) = t1 .
    eq box1(f1) = t1 .

    --- case (1) in the PDF 
    eq box2(T) = t2 .
    eq box2(t) = F .
    eq box2(t2) = F .
    eq box2(f2) = t2 .
    eq box2(f) = F .
    eq box2(F) = F .
    eq box2(t1) = t1 .
    eq box2(f1) = t1 .

    eq box'(T) = T, t2 .
    eq box'(t) = F .
    eq box'(t2) = F .
    eq box'(f2) = T, t2 .
    eq box'(f) = F .
    eq box'(F) = F .
    eq box'(t1) = t1 .
    eq box'(f1) = t1 .

    op valid-case? : Matrix -> Bool .
    ceq valid-case?( (ROWS, [A : VA, C : VC, NEW : V]: ID)) = false 
        if (VA in (t1, f1) and-then (not VC in (t1, f1))) or-else
           (VC in (t1, f1) and-then (not VA in (t1, f1))) .
    eq valid-case?(ROWS) = true [owise] .

    --- Checking arrow for t/f (Two arrows)
    ceq checkArrow([ NEW : V,  ASSG  ] : 1, 
                   (ROWS, 
                    [ NEW : V' ,  ASSG'   ] : ID', 
                    [ NEW : V'',  ASSG''  ] : ID'')) = true
        if V in (t,f) and-then V' in arrow(V) and-then  V'' in arrow2(V) .

    --- The other cases that need only one arrow 
    ceq checkArrow([ NEW : V,  ASSG  ] : 1, 
                   (ROWS, 
                    [ NEW : V' ,  ASSG'   ] : ID')) = true
        if not(V in (t,f)) and-then ( V in (t1,f1) or-else  V' in arrow(V) ) .


    eq checkArrow(ROW, ROWS) = false [owise] .


    op one-not-N : Matrix -> Bool .
    ceq one-not-N(  ([ A : V,  ASSG  ] : ID, ROWS )) = true 
         if not (V in N ) .
    eq one-not-N(ROWS) = false [owise] .


    rl [value'] :  { ROWS, [ ASSG,  A : VA, NEW : ?f ] : 1 }  => 
        if one-not-N(ROWS) then
            { to-box(ROWS), [ ASSG,  A : VA, NEW : box2(VA)  ] : 1 }
        else 
            { to-box(ROWS), [ ASSG,  A : VA, NEW : box1(VA)  ] : 1 }
       fi .

    --- rl [value'] :  A : VA, NEW : ?f'  => A : VA , NEW : ?[ box'(VA) ] .

    op to-box : Matrix -> Matrix .
    eq to-box(empty) = empty .
    eq to-box( (  [ASSG, A : VA, NEW : ?f'  ] : ID , ROWS)) = 
                 ([ASSG, A : VA, NEW : box'(VA) ] : ID), 
                  to-box(ROWS) .
endm 

view K5-Case-Box from TH-BOX to K5-CASE-BOX is endv

mod CHECK-ANALITICITY-BOX is
    pr ANALITICITY-BOX{K5, K5-Case-Box} .
endm


eof 

dsrew  in CHECK-ANALITICITY-BOX  : initBox using guess ! ; depC ; depA ; guess ! ; value' ! ; guess ! ; ignore ; (match no-case ? fail : idle) ; one(simplify) ! ; check .
dsrew  in CHECK-ANALITICITY-BOX  : initBox using guess ! ; depC ; depA ; guess ! ; value' ! ; guess ! ; ignore ; (match no-case ? fail : idle) ; one(simplify) ! ; check ; match FS:FState .


